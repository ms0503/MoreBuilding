plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'ms.sora.gradle.morebuilding'
}

version = "${version}+${libs.minecraft.get().version}"
group = maven_group

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(':morebuilding-common').loom.accessWidenerPath

    mods {
        register("$mod_id") {
            sourceSet sourceSets.main as SourceSet
        }
    }

    runs {
        client {
            name 'Client'
            property 'fabric.log.level', 'debug'
        }

        data {
            inherit server
            name 'Data Generation'
            vmArg '-Dfabric-api.datagen'
            vmArg "-Dfabric-api.datagen.modid=$mod_id"
            vmArg "-Dfabric-api.datagen.output-dir=${file('src/main/generated')}"
            runDir 'build/datagen'
        }

        server {
            name 'Dedicated Server'
            property 'fabric.log.level', 'debug'
        }
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    maven {
        name = 'TerraformersMC'
        url = 'https://maven.terraformersmc.com/releases'
    }
}

dependencies {
    modApi libs.architectury.fabric
    modApi libs.fabric.api
    modApi libs.modmenu
    modImplementation libs.fabric.loader
    modRuntimeOnly libs.cloth.fabric
    include modApi('teamreborn:energy:3.0.0') {
        transitive false
    }
    common(project(path: ':morebuilding-common', configuration: 'namedElements')) {
        transitive false
    }
    shadowCommon(project(path: ':morebuilding-common', configuration: 'transformProductionFabric')) {
        transitive false
    }
}

processResources {
    var replaceProperties = [
        architectury_version_range : architectury_version_range_fabric,
        cloth_config_version_range: cloth_config_version_range_fabric,
        fabric_api_version_range   : fabric_api_version_range,
        fabric_loader_version_range: fabric_loader_version_range,
        java_version_range   : java_version_range,
        minecraft_version_range    : minecraft_version_range_fabric,
        mod_description            : mod_description,
        mod_id                     : mod_id,
        mod_license                : mod_license,
        mod_name                   : mod_name,
        mod_version                : version,
        modmenu_version_range: modmenu_version_range,
        repo_page_url              : repo_page_url
    ]
    inputs.properties replaceProperties
    filesMatching('fabric.mod.json') {
        expand replaceProperties + [project: project]
    }
    filesMatching('en_us.json') {
        expand mod_description: mod_description
    }
    filesMatching('ja_jp.json') {
        expand mod_description: mod_description_ja_jp
    }
}

tasks.register('copyGeneratedResources', Copy) {
    from project(':morebuilding-quilt').file('src/main/generated').absolutePath
    into 'build/resources/main'
}

tasks.withType(Jar).configureEach {
    dependsOn copyGeneratedResources
}

tasks.withType(Javadoc).configureEach {
    dependsOn copyGeneratedResources
}

shadowJar {
    exclude 'architectury.common.json'
    configurations = [
        project.configurations.shadowCommon
    ]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

tasks.register('renameJarForPublication', Zip) {
    dependsOn remapJar
    from remapJar.archiveFile.map {
        zipTree(it)
    }
    archiveExtension = 'jar'
    metadataCharset = 'UTF-8'
    destinationDirectory = base.libsDirectory
    archiveClassifier = name
}

assemble.dependsOn renameJarForPublication

jar {
    archiveClassifier = 'dev'
}

sourcesJar {
    Jar commonSources = project(':morebuilding-common').sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map {
        zipTree(it)
    }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        register('MoreBuildingFabric', MavenPublication) {
            from components.java as SoftwareComponent
            groupId = maven_group
            artifactId = "${archives_base_name}-fabric"
            artifact(jar) {
                classifier = 'dev'
                extension = 'jar'
            }
            artifact(shadowJar) {
                classifier = 'dev-shadow'
                extension = 'jar'
            }
            pom {
                description = mod_description
                developers {
                    it.developer {
                        email = 'ms0503@outlook.com'
                        id = 'ms0503'
                        name = 'Sora Tonami'
                    }
                }
                licenses {
                    it.license {
                        distribution = 'repo'
                        name = mod_license
                        url = "${repo_page_url}/blob/HEAD/LICENSE.md"
                    }
                }
                name = archives_base_name
                resolveStrategy = 1
                scm {
                    connection = "scm:git:${repo_url}"
                    developerConnection = "scm:git:${repo_url}"
                    url = repo_page_url
                }
                url = repo_page_url
            }
        }
    }

    repositories {
        maven {
            credentials {
                password = System.getenv('GITHUB_TOKEN')
                username = 'ms0503'
            }
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/ms0503/more-building'
        }
    }
}

signing {
    sign publishing.publications.MoreBuildingFabric
}
