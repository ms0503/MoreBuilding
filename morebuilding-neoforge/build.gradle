plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'ms.sora.gradle.morebuilding'
}

version = "${version}+${libs.minecraft.get().version}"
group = maven_group

architectury {
    platformSetupLoomIde()
    neoForge {
        platformPackage = 'neoforge'
    }
}

loom {
    accessWidenerPath = project(':morebuilding-common').loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
        mixinConfig 'morebuilding-common.mixins.json'
        mixinConfig 'morebuilding-neoforge.mixins.json'
    }

    mods {
        register("$mod_id") {
            sourceSet sourceSets.main as SourceSet
        }
    }

    runs {
        data {
            data()
            programArg '--all'
            programArgs '--mod', "$mod_id"
            programArgs '--existing', project(':morebuilding-common').file('src/main/resources').absolutePath
            programArgs '--output', file('src/main/generated').absolutePath
            runDir 'build/datagen'
        }
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven {
        name = 'Su5eD'
        url = 'https://maven.su5ed.dev/releases'
    }
}

dependencies {
    forge libs.neoforge
    modApi libs.architectury.forge
    modRuntimeOnly libs.cloth.forge
    common(project(path: ':morebuilding-common', configuration: 'namedElements')) {
        transitive false
    }
    shadowCommon(project(path: ':morebuilding-common', configuration: 'transformProductionNeoForge')) {
        transitive false
    }
}

processResources {
    var replaceProperties = [
        architectury_version_range   : architectury_version_range_forge,
        cloth_config_version_range: cloth_config_version_range_forge,
        minecraft_version_range      : minecraft_version_range_forge,
        mod_description              : mod_description,
        mod_id                       : mod_id,
        mod_license                  : mod_license,
        mod_name                     : mod_name,
        mod_version                  : version,
        neoforge_loader_version_range: neoforge_loader_version_range,
        neoforge_version_range       : neoforge_version_range,
        repo_page_url                : repo_page_url,
        update_json_url              : update_json_url
    ]
    inputs.properties replaceProperties
    filesMatching([
        'META-INF/mods.toml',
        'pack.mcmeta'
    ]) {
        expand replaceProperties + [project: project]
    }
}

tasks.register('copyGeneratedResources', Copy) {
    from project(':morebuilding-quilt').file('src/main/generated').absolutePath
    into 'build/resources/main'
}

tasks.withType(Jar).configureEach {
    dependsOn copyGeneratedResources
}

tasks.withType(Javadoc).configureEach {
    dependsOn copyGeneratedResources
}

shadowJar {
    exclude 'fabric.mod.json'
    exclude 'architectury.common.json'
    configurations = [
        project.configurations.shadowCommon
    ]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

tasks.register('renameJarForPublication', Zip) {
    dependsOn remapJar
    from remapJar.archiveFile.map {
        zipTree(it)
    }
    archiveExtension = 'jar'
    metadataCharset = 'UTF-8'
    destinationDirectory = base.libsDirectory
    archiveClassifier = name
}

assemble.dependsOn renameJarForPublication

jar {
    archiveClassifier = 'dev'
}

sourcesJar {
    Jar commonSources = project(':morebuilding-common').sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map {
        zipTree(it)
    }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        register('MoreBuildingNeoForge', MavenPublication) {
            from components.java as SoftwareComponent
            groupId = maven_group
            artifactId = "${archives_base_name}-neoforge"
            artifact(jar) {
                classifier = 'dev'
                extension = 'jar'
            }
            artifact(shadowJar) {
                classifier = 'dev-shadow'
                extension = 'jar'
            }
            pom {
                description = mod_description
                developers {
                    it.developer {
                        email = 'ms0503@outlook.com'
                        id = 'ms0503'
                        name = 'Sora Tonami'
                    }
                }
                licenses {
                    it.license {
                        distribution = 'repo'
                        name = mod_license
                        url = "${repo_page_url}/blob/HEAD/LICENSE.md"
                    }
                }
                name = archives_base_name
                resolveStrategy = 1
                scm {
                    connection = "scm:git:${repo_url}"
                    developerConnection = "scm:git:${repo_url}"
                    url = repo_page_url
                }
                url = repo_page_url
            }
        }
    }

    repositories {
        maven {
            credentials {
                password = System.getenv('GITHUB_TOKEN')
                username = 'ms0503'
            }
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/ms0503/more-building'
        }
    }
}

signing {
    sign publishing.publications.MoreBuildingNeoForge
}
