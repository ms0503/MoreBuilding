import com.modrinth.minotaur.dependencies.DependencyType
import com.modrinth.minotaur.dependencies.ModDependency

plugins {
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.modrinth.minotaur' version '2.+'
    id 'dev.architectury.loom' version '1.3-SNAPSHOT' apply false
    id 'idea'
    id 'java'
    id 'io.github.pacifistmc.forgix' version '1.+'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
    project {
        languageLevel = '17'
    }
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
    it.options.release.set(17)
}

forgix {
    group = maven_group
    mergedJarName = "${archives_base_name}-${version}.jar"
    outputDir = 'build/libs/merged'

    fabric {
        jarLocation = "build/libs/${archives_base_name}-${version}.jar"
        projectName = 'morebuilding-fabric'
    }

    forge {
        jarLocation = "build/libs/${archives_base_name}-${version}.jar"
        projectName = 'morebuilding-forge'
    }

    custom {
        jarLocation = "build/libs/${archives_base_name}-${version}.jar"
        projectName = 'morebuilding-neoforge'
    }

    quilt {
        jarLocation = "build/libs/${archives_base_name}-${version}.jar"
        projectName = 'morebuilding-quilt'
    }
}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        maven {
            name = 'Quilt'
            url = 'https://maven.quiltmc.org/repository/release'
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        mappings "org.quiltmc:quilt-mappings:1.20.1+build.23:intermediary-v2"
    }
}

tasks.mergeJars.dependsOn tasks.build
tasks.modrinth.dependsOn.addAll(tasks.clean, tasks.build, tasks.mergeJars)

modrinth {
    changelog = '''
        This mod requires [Architectury API](https://modrinth.com/mod/architectury-api) (in all) and [Fabric API](https://modrinth.com/mod/fabric-api) (in Fabric) or [Quilt Standard Library](https://modrinth.com/mod/qsl) (in QuiltMC).
        Click [here](https://github.com/ms0503/more-building/commits/1.20.1) for changelog.

        [Architectury API](https://modrinth.com/mod/architectury-api)(全ての場合)と[Fabric API](https://modrinth.com/mod/fabric-api)(Fabricの場合)または[Quilt Standard Library](https://modrinth.com/mod/qsl)(QuiltMCの場合)が前提MODとして必要です。
        チェンジログは[こちら](https://github.com/ms0503/more-building/commits/1.20.1)から読むことが出来ます。
    '''
    debugMode = false
    dependencies.addAll([
        new ModDependency('P7dR8mSH', DependencyType.REQUIRED), // Fabric API
        new ModDependency('qvIfYCYJ', DependencyType.REQUIRED), // QFAPI/QSL
        new ModDependency('lhGA9TYQ', DependencyType.REQUIRED)  // Architectury API
    ])
    gameVersions.add(minecraft_version)
    loaders.addAll('fabric', 'forge', 'neoforge', 'quilt')
    projectId = modrinth_project_id
    syncBodyFrom = rootProject.file('README.md').text
    token = System.getenv('MODRINTH_TOKEN')
    uploadFile = "${forgix.outputDir}/${forgix.mergedJarName}"
    versionName = "v${version}"
    versionNumber.set(version as String)
    versionType = 'alpha'
}
