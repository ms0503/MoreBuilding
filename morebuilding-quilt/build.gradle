import net.fabricmc.loom.task.RunGameTask

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'ms.sora.gradle.morebuilding'
}

version = "${version}+${libs.minecraft.get().version}"
group = maven_group

architectury {
    platformSetupLoomIde()
    loader('quilt') {
        platformPackage = 'quilt'
    }
}

loom {
    accessWidenerPath = project(':morebuilding-common').loom.accessWidenerPath

    mods {
        register("$mod_id") {
            sourceSet sourceSets.main as SourceSet
        }
    }

    runs {
        data {
            inherit server
            name 'Data Generation'
            vmArg '-Dfabric-api.datagen'
            vmArg "-Dfabric-api.datagen.modid=$mod_id"
            vmArg "-Dfabric-api.datagen.output-dir=${file('src/main/generated')}"
            runDir 'build/datagen'
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs += [
                'src/main/generated'
            ]
        }
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentQuilt.extendsFrom common
}

repositories {
    maven {
        name = 'TerraformersMC'
        url = 'https://maven.terraformersmc.com/releases'
    }
}

dependencies {
    modApi(libs.architectury.fabric) {
        exclude group: 'net.fabricmc'
        exclude group: 'net.fabricmc.fabric-api'
    }
    modApi(libs.modmenu) {
        exclude group: 'net.fabricmc'
        exclude group: 'net.fabricmc.fabric-api'
    }
    modApi libs.quilt.loader
    modApi libs.quilt.api
    modRuntimeOnly(libs.cloth.fabric) {
        exclude group: 'net.fabricmc'
        exclude group: 'net.fabricmc.fabric-api'
    }
    include modApi('teamreborn:energy:3.0.0') {
        transitive false
    }
    common(project(path: ':morebuilding-common', configuration: 'namedElements')) {
        transitive false
    }
    shadowCommon(project(path: ':morebuilding-common', configuration: 'transformProductionQuilt')) {
        transitive false
    }
}

processResources {
    var replaceProperties = [
        architectury_version_range: architectury_version_range_fabric,
        cloth_config_version_range: cloth_config_version_range_fabric,
        maven_group               : maven_group,
        minecraft_version_range   : minecraft_version_range_fabric,
        mod_description           : mod_description,
        mod_id                    : mod_id,
        mod_license               : mod_license,
        mod_name                  : mod_name,
        mod_version               : version,
        quilt_api_version_range   : quilt_api_version_range,
        quilt_loader_version_range: quilt_loader_version_range,
        repo_page_url             : repo_page_url
    ]
    inputs.properties replaceProperties
    filesMatching('quilt.mod.json') {
        expand replaceProperties + [project: project]
    }
}

shadowJar {
    exclude 'architectury.common.json'
    configurations = [
        project.configurations.shadowCommon
    ]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

tasks.register('renameJarForPublication', Zip) {
    dependsOn remapJar
    from remapJar.archiveFile.map {
        zipTree(it)
    }
    archiveExtension = 'jar'
    metadataCharset = 'UTF-8'
    destinationDirectory = base.libsDirectory
    archiveClassifier = name
}

assemble.dependsOn renameJarForPublication

jar {
    archiveClassifier = 'dev'
}

sourcesJar {
    Jar commonSources = project(':morebuilding-common').sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map {
        zipTree(it)
    }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

tasks.register('prepareRunData', Copy) {
    from project(':morebuilding-common').file('src/main/resources').listFiles().absolutePath
    into 'build/resources/main'
}

tasks.named('runData', RunGameTask) {
    dependsOn prepareRunData as Copy
}

publishing {
    publications {
        register('MoreBuildingQuilt', MavenPublication) {
            from components.java as SoftwareComponent
            groupId = maven_group
            artifactId = "${archives_base_name}-quilt"
            artifact(jar) {
                classifier = 'dev'
                extension = 'jar'
            }
            artifact(shadowJar) {
                classifier = 'dev-shadow'
                extension = 'jar'
            }
            pom {
                description = mod_description
                developers {
                    it.developer {
                        email = 'ms0503@outlook.com'
                        id = 'ms0503'
                        name = 'Sora Tonami'
                    }
                }
                licenses {
                    it.license {
                        distribution = 'repo'
                        name = mod_license
                        url = "${repo_page_url}/blob/HEAD/LICENSE.md"
                    }
                }
                name = archives_base_name
                resolveStrategy = 1
                scm {
                    connection = "scm:git:${repo_url}"
                    developerConnection = "scm:git:${repo_url}"
                    url = repo_page_url
                }
                url = repo_page_url
            }
        }
    }

    repositories {
        maven {
            credentials {
                password = System.getenv('GITHUB_TOKEN')
                username = 'ms0503'
            }
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/ms0503/more-building'
        }
    }
}

signing {
    sign publishing.publications.MoreBuildingQuilt
}
