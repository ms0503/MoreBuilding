import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'ms.sora.gradle.morebuilding'
}

version = "${version}+${libs.minecraft.get().version}"
group = maven_group

architectury {
    common(enabled_platforms.split(',')) {
        it.platformPackage 'neoforge', 'neoforge'
        it.platformPackage 'quilt', 'quilt'
    }
}

loom {
    accessWidenerPath = file('src/main/resources/morebuilding.accesswidener')
}

repositories {
}

dependencies {
    modApi libs.architectury
    modApi(libs.cloth) {
        exclude group: 'net.fabricmc.fabric-api'
    }
}

tasks.register('expandInfo', Copy) {
    doFirst {
        delete 'src/main/java/ms/sora/mod/morebuilding/common/MBInfo.java'
    }
    from 'MBInfo.java'
    into 'src/main/java/ms/sora/mod/morebuilding/common'
    expand mod_description: mod_description, mod_id: mod_id, mod_name: mod_name, mod_version: version
}

compileJava.dependsOn expandInfo
sourcesJar.dependsOn expandInfo

tasks.register('renameJarForPublication', Zip) {
    dependsOn remapJar as RemapJarTask
    from remapJar.archiveFile.map {
        zipTree(it)
    }
    archiveExtension = 'jar'
    metadataCharset = 'UTF-8'
    destinationDirectory = base.libsDirectory
    archiveClassifier = name
}

assemble.dependsOn renameJarForPublication

jar {
    archiveClassifier = 'dev'
}

artifacts {
    archives remapJar
}

publishing {
    publications {
        register('MoreBuildingCommon', MavenPublication) {
            from components.java as SoftwareComponent
            groupId = maven_group
            artifactId = "${archives_base_name}-common"
            artifact(jar) {
                classifier = 'dev'
                extension = 'jar'
            }
            pom {
                description = mod_description
                developers {
                    it.developer {
                        email = 'ms0503@outlook.com'
                        id = 'ms0503'
                        name = 'Sora Tonami'
                    }
                }
                licenses {
                    it.license {
                        distribution = 'repo'
                        name = mod_license
                        url = "${repo_page_url}/blob/HEAD/LICENSE.md"
                    }
                }
                name = archives_base_name
                resolveStrategy = 1
                scm {
                    connection = "scm:git:${repo_url}"
                    developerConnection = "scm:git:${repo_url}"
                    url = repo_page_url
                }
                url = repo_page_url
            }
        }
    }

    repositories {
        maven {
            credentials {
                password = System.getenv('GITHUB_TOKEN')
                username = 'ms0503'
            }
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/ms0503/more-building'
        }
    }
}

signing {
    sign publishing.publications.MoreBuildingCommon
}
